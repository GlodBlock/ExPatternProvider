buildscript {
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle' version '[6.0.13, 6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
}

version = "${mod_version}"
group = 'com.glodblock.github.appflux'

base {
    archivesName = mod_name
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: mapping_channel, version: mapping_version
    copyIdeResources = true
    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', mod_id
            arg "-mixin.config=" + "mixins.appflux.json"
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
            arg "-mixin.config=" + "mixins.appflux.json"
        }
        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            workingDirectory project.file('run-data')
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url = "https://maven.blamejared.com/"
    }
    maven {
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "Modmaven"
        url = "https://modmaven.dev/"
    }
}

dependencies {
    minecraft "net.neoforged:forge:${minecraft_version}-${forge_version}"
    implementation fg.deobf("curse.maven:glodium-957920:5024300")
    implementation fg.deobf("appeng:appliedenergistics2-forge:15.0.12")
    compileOnly fg.deobf("curse.maven:wireless-chargers-510656:4591907")
    compileOnly fg.deobf("mekanism:Mekanism:1.20.1-10.4.0.14:all")
    compileOnly fg.deobf("mezz.jei:jei-1.20.1-forge:15.0.0.12")
    // for some reason this deobf jar fails to load at file correctly
    compileOnly fg.deobf("curse.maven:jade-324717:4768593")
    annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'
    // runtime test
    runtimeOnly fg.deobf("mezz.jei:jei-1.20.1-forge:15.0.0.12")
    runtimeOnly fg.deobf("curse.maven:jade-324717:4768593")
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
            neo_version: forge_version, forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_authors: mod_authors, mod_description: mod_description,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : '1',
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'TweakClass': "org.spongepowered.asm.launch.MixinTweaker",
                'TweakOrder': 0,
                'MixinConfigs': "mixins.appflux.json"
        ])
    }
    finalizedBy 'reobfJar'
}

mixin {
    add sourceSets.main, "mixins.appflux.refmap.json"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
